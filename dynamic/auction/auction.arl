archetype auction(
    auctioneer : role,
    initial_bid : tez,
    deadline : date
)

asset bid identified by bidder {
    bidder : address;
    value  : tez;
}

variable highest_bid : tez = initial_bid

states =
  | Created initial
  | Completed with { i2 : now >= deadline; }

transition complete () {
    from Created
    to Completed
    with effect {
        for b in bid.select(value <> highest_bid) do
            transfer bid[b].value to b;
        done;
        transfer balance to auctioneer;
    }    
}

entry place_bid () {
    require {
        place_bid_c1 : state = Created;
        place_bid_c2 : transferred > highest_bid
    }

    effect {
        bid.add({
            bidder = caller;
            value = transferred
        });
        highest_bid := max(transferred, highest_bid);
    }
}

specification {
    contract invariant s1 {
        forall b in bid, b.bidder <> auctioneer
    }

    contract invariant s2 {
        forall b in bid, balance > b.value
    }
}