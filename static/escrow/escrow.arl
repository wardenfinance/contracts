archetype escrow(
  title : string,
  description : string,
  buyer : role,
  seller : role,
  oracle : role,
  price : tez,
  fee : tez,
  deadline : date
)

states =
 | Created initial
 | Aborted   with { i1 : balance = 0tz; }
 | Confirmed with { i2 : balance = price; }
 | Canceled  with { i3 : balance = 0tz; }
 | Completed with { i4 : balance = 0tz; }

transition abort () {
  called by buyer or seller

  from Created
  to Aborted
}

transition confirm () {
  from Created
  to Confirmed when { balance = price + fee }
}

transition complete () {
  called by oracle

  from Confirmed
  to Completed when { now < deadline }
  with effect {
    transfer price to seller;
    transfer fee to oracle;
  }
}

transition cancel () {
  called by oracle

  from Confirmed
  to Canceled
  with effect {
    transfer price to buyer;
  }
}

specification {
  contract invariant s1 {
    buyer <> seller
  }

  contract invariant s2 {
    buyer <> oracle
  }

  contract invariant s3 {
    balance <= price + fee
  }

  contract invariant s4 {
    now <= deadline
  }
}